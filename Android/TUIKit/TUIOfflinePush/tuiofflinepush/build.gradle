plugins {
    id 'com.android.library'
}

ext {
    // 是否要编译 uniapp 推送原生插件，默认为 false
    // Whether to compile uniapp and push native plugins，default is false
    UNIAPP_MODEL = false
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    namespace "com.tencent.qcloud.tim.tuiofflinepush"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        //buildConfigField "boolean", "UNIAPP_MODEL", String.valueOf(UNIAPP_MODEL)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (!UNIAPP_MODEL) {
                java.srcDirs = [
                        'src/main/java',
                        'src/main/native/java'
                ]
            } else {
                java.srcDirs = [
                        'src/main/java',
                        'src/main/uniapp/java'
                ]
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.code.gson:gson:2.9.1'

    implementation 'com.tencent.timpush:huawei:7.7.5282'
    implementation 'com.tencent.timpush:fcm:7.7.5282'
    implementation 'com.tencent.timpush:xiaomi:7.7.5282'
    implementation 'com.tencent.timpush:oppo:7.7.5282'
    implementation 'com.tencent.timpush:vivo:7.7.5282'
    implementation 'com.tencent.timpush:honor:7.7.5282'
    implementation 'com.tencent.timpush:meizu:7.7.5282'

    if (!UNIAPP_MODEL) {
        def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()
        println "all TUIOfflinePush projects : {$projects}"
        if (projects.contains("tuicore")) {
            api project(':tuicore')
        } else {
            api "com.tencent.imsdk:imsdk-plus:7.7.5282"
            api 'com.tencent.liteav.tuikit:tuicore:7.7.5285'
        }
    } else {
        implementation 'com.alibaba:fastjson:1.1.46.android'
        compileOnly fileTree(dir: 'libs', include: ['uniapp-v8-release.aar'])
    }
}