import org.gradle.util.VersionNumber

apply plugin: 'com.android.library'
def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()

android {
    namespace "com.tencent.cloud.tuikit.roomkit"
    compileSdk 30

    defaultConfig {
        minSdkVersion 19
        multiDexEnabled = true
        targetSdkVersion 30
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def roomSourceJavaVersion = JavaVersion.VERSION_1_8
    VersionNumber currentGradleVersion = VersionNumber.parse(gradle.gradleVersion)
    if (currentGradleVersion.major >= 8) {
        roomSourceJavaVersion = JavaVersion.VERSION_17
    }

    compileOptions {
        sourceCompatibility roomSourceJavaVersion
        targetCompatibility roomSourceJavaVersion
    }
}

dependencies {
    api fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    implementation "de.hdodenhof:circleimageview:3.1.0"
    implementation "com.github.bumptech.glide:glide:4.12.0"
    implementation 'com.google.zxing:core:3.5.3'
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation "com.google.code.gson:gson:2.9.1"
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation 'androidx.core:core:1.0.2'
    implementation 'com.tencentcloudapi:tencentcloud-sdk-java:3.1.1113'

    implementation project(':timcommon')

    api rootProject.getProperties().containsKey("roomEngineSdk") ? rootProject.ext.roomEngineSdk : "io.trtc.uikit:rtc_room_engine:2.8.3.54"
    implementation rootProject.getProperties().containsKey("liteavSdk") ? rootProject.ext.liteavSdk : "com.tencent.liteav:LiteAVSDK_TRTC:12.2.0.15065"
    if (projects.contains("tuicore")) {
        api project(':tuicore')
    } else {
        api "com.tencent.liteav.tuikit:tuicore:8.4.6667"
        implementation rootProject.getProperties().containsKey("imSdk") ? rootProject.ext.imSdk : "com.tencent.imsdk:imsdk-plus:8.4.6667"
    }
    if (projects.contains("common")) {
        implementation project(':common')
    } else {
        implementation 'io.trtc.uikit:common:latest.release'
    }
}
