import org.gradle.util.VersionNumber

apply plugin: 'com.android.library'
def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()

android {
    namespace "com.tencent.cloud.tuikit.roomkit"
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def roomSourceJavaVersion = JavaVersion.VERSION_1_8
    VersionNumber currentGradleVersion = VersionNumber.parse(gradle.gradleVersion)
    if (currentGradleVersion.major >= 8) {
        roomSourceJavaVersion = JavaVersion.VERSION_17
    }

    compileOptions {
        sourceCompatibility roomSourceJavaVersion
        targetCompatibility roomSourceJavaVersion
    }
}

dependencies {
    api fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    implementation "de.hdodenhof:circleimageview:3.1.0"
    implementation "com.github.bumptech.glide:glide:4.12.0"
    implementation 'me.dm7.barcodescanner:zxing:1.8.4'
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation 'androidx.core:core:1.0.2'

    implementation project(':timcommon')

    api rootProject.getProperties().containsKey("roomEngineSdk") ? rootProject.ext.roomEngineSdk : "com.tencent.liteav.tuikit:tuiroomengine:2.2.2.27"
    implementation rootProject.getProperties().containsKey("liteavSdk") ? rootProject.ext.liteavSdk : "com.tencent.liteav:LiteAVSDK_TRTC:11.6.0.13641"
    if (projects.contains("tuicore")) {
        api project(':tuicore')
    } else {
        api "com.tencent.liteav.tuikit:tuicore:7.9.5666"
        implementation rootProject.getProperties().containsKey("imSdk") ? rootProject.ext.imSdk : "com.tencent.imsdk:imsdk-plus:7.9.5666"
    }
}
