import org.gradle.util.VersionNumber

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdkVersion 34
    namespace "com.tencent.qcloud.tuikit.tuicallkit"

    defaultConfig {
        versionName "1.0"
        versionCode 1
        minSdkVersion 19
        targetSdkVersion 34
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    def callSourceJavaVersion = JavaVersion.VERSION_1_8
    VersionNumber currentGradleVersion = VersionNumber.parse(gradle.gradleVersion)
    if (currentGradleVersion.major >= 8) {
        callSourceJavaVersion = JavaVersion.VERSION_17
    }

    compileOptions {
        sourceCompatibility callSourceJavaVersion
        targetCompatibility callSourceJavaVersion
    }
}

dependencies {

    api 'androidx.appcompat:appcompat:1.3.1'
    api 'com.google.android.material:material:1.4.0'
    api 'androidx.recyclerview:recyclerview:1.2.1'
    api 'androidx.constraintlayout:constraintlayout:1.1.3'
    api 'com.github.bumptech.glide:glide:4.12.0'
    def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()
    println "all projects : {$projects}"
    if (projects.contains("tuicore")) {
        api project(':tuicore')
    } else {
        api "com.tencent.imsdk:imsdk-plus:8.1.6103"
        api "com.tencent.liteav.tuikit:tuicore:8.1.6103"
    }

    if (projects.contains("tuicallengine")) {
        api project(':tuicallengine')
    } else {
        api "com.google.code.gson:gson:2.9.1"
        api rootProject.getProperties().containsKey("liteavSdk") ? rootProject.ext.liteavSdk : "com.tencent.liteav:LiteAVSDK_Professional:11.8.0.14176"
        api 'com.tencent.liteav.tuikit:tuicallengine:2.5.0.1020'
    }
}
