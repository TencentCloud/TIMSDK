apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    signingConfigs {
        release {
            storeFile file("$System.env.KEYSTORE_PATH")
            storePassword "$System.env.STOREPASS"
            keyAlias "$System.env.ALIAS"
            keyPassword "$System.env.KEYPASS"
        }
    }

    defaultConfig {
        applicationId "com.tencent.qcloud.tim.tuikit"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        archivesBaseName = "TUIKitDemo"
        multiDexEnabled = true

        ndk {
            abiFilters = []
            abiFilters.addAll(rootProject.ext.abiFilters)
        }

        manifestPlaceholders = [
                // TPNS 推送服务 接入点域名、accessId、accessKey
                /** 如果您的应用服务接入点为广州，SDK 默认实现该配置。
                 *  其他服务接入点域名如下：
                 *  上海：tpns.sh.tencent.com
                 *  新加坡：tpns.sgp.tencent.com
                 *  中国香港：tpns.hk.tencent.com
                 */
                "XG_SERVER_SUFFIX" : "",
                "XG_ACCESS_ID" : "",
                "XG_ACCESS_KEY" : "",

                // 可选：配置首次启动应用时不自动启动推送服务进程，直到调用 XGPushManager.registerPush 接口才开始启动
                "XG_SERVICE_PULL_UP_OFF" : "true",
                "VIVO_APPKEY" : "",
                "VIVO_APPID" : ""
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
//            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 主题资源文件夹
    sourceSets {
        main {
            res.srcDirs += "src/main/res-light"
            res.srcDirs += "src/main/res-lively"
            res.srcDirs += "src/main/res-serious"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar','*.aar'], dir: 'libs')
    implementation fileTree(include: ['*.jar','*.aar'], dir: '../libs')

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.android.support:design:28.0.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'com.google.android.material:material:1.4.0'
    // Optional, if you use support library fragments:

    // tpns推送,最新版本号可参考 [Android SDK 发布动态](https://cloud.tencent.com/document/product/548/44520)
    // 主包
    implementation 'com.tencent.tpns:tpns:1.3.1.1-release'
    // Google FCM
    implementation "com.tencent.tpns:fcm:1.3.1.1-release"
    // google 云消息传递
    implementation ('com.google.firebase:firebase-messaging:19.0.1')
    // 小米
    implementation "com.tencent.tpns:xiaomi:1.3.1.1-release"
    // 魅族
    implementation "com.tencent.tpns:meizu:1.3.1.1-release"
    // OPPO
    implementation "com.tencent.tpns:oppo:1.3.1.1-release"
    // vivo
    implementation "com.tencent.tpns:vivo:1.3.1.1-release"
    // 华为
    implementation 'com.tencent.tpns:huawei:1.3.1.1-release'
    implementation 'com.huawei.hms:push:5.0.2.300'

    // bugly sdk
    implementation 'com.tencent.bugly:crashreport:latest.release'
    implementation 'com.tencent.bugly:nativecrashreport:latest.release'

    implementation 'com.github.bumptech.glide:glide:4.12.0'

    api project(':tuiconversation')
    api project(':tuicontact')
    api project(':tuichat')
    api project(':tuisearch')
    api project(':tuigroup')
    api project(':tuicalling')
}
