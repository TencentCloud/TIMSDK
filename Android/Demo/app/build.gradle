apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    signingConfigs {
        release {
            storeFile file("$System.env.KEYSTORE_PATH")
            storePassword "$System.env.STOREPASS"
            keyAlias "$System.env.ALIAS"
            keyPassword "$System.env.KEYPASS"
        }
    }

    defaultConfig {
        applicationId "com.tencent.qcloud.tim.tuikit"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        archivesBaseName = "TUIKitDemo"
        multiDexEnabled = true
        ndk {
            abiFilters = []
            abiFilters.addAll(rootProject.ext.abiFilters)
        }

        /*  集成组件 TUIOfflinePush 离线推送功能需要配置下厂商 vivo 的应用信息，不需要离线推送功能删除即可。
         *
         *  "VIVO_APPKEY" : 厂商推送平台注册应用的 AppKey
         *  "VIVO_APPID" : 厂商推送平台注册应用的 AppID
         */
        manifestPlaceholders = [
                "VIVO_APPKEY" : " ",
                "VIVO_APPID" : " ",
                "HONOR_APPID" : " "
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
//            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 主题资源文件夹
    sourceSets {
        main {
            res.srcDirs += "src/main/res-light"
            res.srcDirs += "src/main/res-lively"
            res.srcDirs += "src/main/res-serious"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar','*.aar'], dir: 'libs')
    implementation fileTree(include: ['*.jar','*.aar'], dir: '../libs')
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'com.android.support:design:28.0.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'com.google.android.material:material:1.4.0'
    // Optional, if you use support library fragments:

    // bugly sdk
    implementation 'com.tencent.bugly:crashreport:latest.release'
    implementation 'com.tencent.bugly:nativecrashreport:latest.release'

    implementation 'com.github.bumptech.glide:glide:4.12.0'

    def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()
    println "all projects : {$projects}"


    api project(':tuicommunity')
    api project(':tuiconversation')
    api project(':tuicontact')
    api project(':tuichat')
    api project(':tuisearch')
    api project(':tuigroup')
    api project(':tuicallkit')
    api project(':tuiofflinepush')
    api project(':tuitranslationplugin')

    if (projects.contains("tuigroupnoteplugin")) {
        api project(':tuigroupnoteplugin')
    } else {
        api "com.tencent.imsdk:tuigroupnote-plugin:7.4.4643"
    }

    if (projects.contains("tuipollplugin")) {
        api project(':tuipollplugin')
    } else {
        api "com.tencent.imsdk:tuipoll-plugin:7.4.4643"
    }

    if (projects.contains("tuiconversationmarkplugin")) {
        api project(':tuiconversationmarkplugin')
    } else {
        api "com.tencent.imsdk:tuiconversationmark-plugin:7.4.4643"
    }

    if (projects.contains("tuiconversationgroupplugin")) {
        api project(':tuiconversationgroup-plugin')
    } else {
        api "com.tencent.imsdk:tuiconversationgroup-plugin:7.4.4643"
    }

    api project(':tuiroomkit')
}
