// Top-level build file where you can add configuration options common to all sub-projects/modules.

import org.gradle.util.GradleVersion


buildscript {
    repositories {
        mavenCentral()
        maven { url "https://mirrors.tencent.com/nexus/repository/maven-public/" }
        maven { url "https://mirrors.tencent.com/repository/maven/liteavsdk/" }
        maven { url 'https://developer.huawei.com/repo/' }
        maven { url 'https://developer.hihonor.com/repo' }
    }
    dependencies {
        def currentGradleVersion = GradleVersion.current()

        if (currentGradleVersion >= GradleVersion.version('8.9')) {
            classpath 'com.android.tools.build:gradle:8.7.0'
        } else if (currentGradleVersion >= GradleVersion.version('8.6')) {
            classpath 'com.android.tools.build:gradle:8.4.0'
        } else if (currentGradleVersion >= GradleVersion.version('8.0')) {
            classpath 'com.android.tools.build:gradle:8.0.0'
        } else if (currentGradleVersion >= GradleVersion.version('7.2')) {
            classpath 'com.android.tools.build:gradle:7.0.0'
        } else if (currentGradleVersion >= GradleVersion.version('6.7.1')) {
            classpath 'com.android.tools.build:gradle:4.2.0'
        }
        if (currentGradleVersion >= GradleVersion.version('8.0')) {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.0"
        } else {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.21"
        }

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url "https://mirrors.tencent.com/nexus/repository/maven-public/" }
        maven { url "https://mirrors.tencent.com/repository/maven/liteavsdk/" }
        maven { url 'https://developer.huawei.com/repo/' }
        maven { url 'https://developer.hihonor.com/repo' }
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charset', 'UTF-8')
    }

}

subprojects {
    tasks.withType(Javadoc).all { enabled = false }
}

ext {
    compileSdkVersion = 34
    minSdkVersion = 23
    targetSdkVersion = 34
    versionCode = 1
    versionName = "8.9.7511"
    abiFilters = ['armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64']
    //['armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64']
    arguments = "-DANDROID_STL=c++_static"
    cppFlags = "-std=c++11"

    liteavSdk = "com.tencent.liteav:LiteAVSDK_Professional:12.8+"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
